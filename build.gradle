buildscript {
    repositories {
        maven { url "https://plugins.gradle.org/m2/" }
        maven { url "https://repo.grails.org/grails/core" }
    }
    dependencies {
        classpath "org.grails:grails-gradle-plugin:$grailsGradlePluginVersion"
        classpath 'io.github.groovylang.groovydoc:groovydoc-gradle-plugin:1.0.1'
    }
}
plugins {
    id 'org.asciidoctor.jvm.convert' version '3.3.2'
    id 'java-library'
}

version project.projectVersion
group 'org.grails.plugins'

apply plugin: 'org.grails.grails-plugin'
apply plugin: 'org.grails.grails-gsp'
apply plugin: "org.grails.grails-doc"
apply plugin: "org.grails.internal.grails-plugin-publish"

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenCentral()
    maven { url "https://repo.grails.org/grails/core" }
}

dependencies {
    compileOnly 'org.springframework.boot:spring-boot-starter-logging'
    compileOnly "org.springframework.boot:spring-boot-starter-actuator"
    compileOnly "org.springframework.boot:spring-boot-autoconfigure"
    compileOnly "org.springframework.boot:spring-boot-starter-tomcat"

    compileOnly "org.grails:grails-web-boot"
	compileOnly "org.grails:grails-dependencies"

    api "org.grails:scaffolding-core:$grailsScaffoldingVersion"

    testImplementation "org.grails:grails-web-testing-support"
    testImplementation "org.grails:grails-gorm-testing-support"

    testImplementation 'org.javassist:javassist:3.29.0-GA'
    testImplementation "org.codehaus.groovy:groovy-dateutil"
    testImplementation "cglib:cglib-nodep:2.2.2"
    testImplementation("org.jodd:jodd-wot:$joddWotVersion") {
        exclude module: 'slf4j-api'
        exclude module: 'asm'
    }
}

tasks.withType(Test).configureEach {
    useJUnitPlatform()
}

grailsPublish {
    userOrg = "grails"
    githubSlug = 'gpc/fields'
    license {
        name = 'Apache-2.0'
    }
    title = "Grails Fields Plugin"
    desc = "Fields integration for Grails"
    developers = [robfletcher: 'Rob Fletcher', graemerocher: "Graeme Rocher", sdelamo: "Sergio del Amo", sbglasius: 'SÃ¸ren Berg Glasius']
}

asciidoctor {
    resources {
        from('src/docs/images')
        into "./images"
    }
    sources {
        include 'index.adoc'
    }
    attributes 'experimental': 'true',
            'compat-mode': 'true',
            'toc': 'left',
            'icons': 'font',
            'version': project.projectVersion,
            'sourcedir': 'src/main/groovy'
    baseDirFollowsSourceDir()
    outputDir = "${buildDir}/asciidoc"
}

tasks.register('apiDocs', Copy) {
    from groovydoc.outputs.files
    into file("${buildDir}/asciidoc/api")
}

tasks.named('jar', Jar) {
    it.archiveClassifier = ''
}

asciidoctor.dependsOn(apiDocs)
